apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
version = '0.1.0-SNAPSHOT'
group = 'com.spiddekauga'

sourceSets.main.java.srcDirs = [ "src/" ]

jar {
	manifest {
		attributes	'Implementation-Title': 'Spiddekauga Utils',
					'Implementation-Version': version
	}
}

ext {
	artifactId = 'spiddekauga-utils'
}

task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

artifacts {
	archives sourceJar
}

configurations {
	deployerJars
}

repositories {
	mavenCentral()
}

dependencies {
	compile "com.esotericsoftware:kryo:3.0.0"
	deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

install {
	repositories.mavenInstaller {
		pom.artifactId = project.artifactId
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			pom.artifactId = 'spiddekauga-utils'
			repository(url: "scp://senth.org/mnt/lvm/www/maven.senth.org/releases") {
				authentication(userName: "senth")
			}
			snapshotRepository(url: "scp://senth.org/mnt/lvm/www/maven.senth.org/snapshots") {
				authentication(userName: "senth")
			}
		}
	}
}
